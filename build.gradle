plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.hacom.telecom'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	implementation('org.springframework.boot:spring-boot-starter-data-mongodb-reactive') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	implementation('org.springframework.boot:spring-boot-starter-webflux') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	// Log4j2  with yaml support
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	
	implementation('org.springframework.boot:spring-boot-starter-actuator') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	// Micrometer Prometheus registry
	implementation 'io.micrometer:micrometer-registry-prometheus'
	
	// gRPC dependencies
	implementation('net.devh:grpc-server-spring-boot-starter:3.1.0.RELEASE') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	implementation 'io.grpc:grpc-protobuf:1.60.0'
	implementation 'io.grpc:grpc-stub:1.60.0'
	implementation 'io.grpc:grpc-netty-shaded:1.60.0'
	compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
	
	// Akka classic dependencies
	implementation 'com.typesafe.akka:akka-actor_2.13:2.6.20'
	implementation 'com.typesafe.akka:akka-slf4j_2.13:2.6.20'
	
	// Cloudhopper SMPP dependencies
	implementation 'com.cloudhopper:ch-smpp:5.0.9'
	implementation 'com.cloudhopper:ch-commons-charset:3.0.2'
	
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.25.1"
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.60.0'
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}
